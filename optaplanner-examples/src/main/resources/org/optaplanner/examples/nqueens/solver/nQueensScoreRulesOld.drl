/*
 * Copyright 2010 JBoss Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package org.optaplanner.examples.nqueens.solver;
    dialect "java"

import org.optaplanner.core.api.score.buildin.simple.SimpleScoreHolder;
import org.optaplanner.core.impl.score.constraint.UnweightedConstraintOccurrence;
import org.optaplanner.core.impl.score.constraint.ConstraintType;

import org.optaplanner.examples.nqueens.domain.Queen;

global SimpleScoreHolder scoreHolder;

// ############################################################################
// Hard constraints
// ############################################################################

rule "multipleQueensHorizontal"
    when
        $q1 : Queen($id : id, row != null, $i : rowIndex)
        $q2 : Queen(id > $id, rowIndex == $i)
    then
        insertLogical(new UnweightedConstraintOccurrence("multipleQueensHorizontal", $q1, $q2));
end

// multipleQueensVertical is build-in

rule "multipleQueensAscendingDiagonal"
    when
        $q1 : Queen($id : id, row != null, $i : ascendingDiagonalIndex)
        $q2 : Queen(id > $id, ascendingDiagonalIndex == $i)
    then
        insertLogical(new UnweightedConstraintOccurrence("multipleQueensAscendingDiagonal", $q1, $q2));
end

rule "multipleQueensDescendingDiagonal"
    when
        $q1 : Queen($id : id, row != null, $i : descendingDiagonalIndex)
        $q2 : Queen(id > $id, descendingDiagonalIndex == $i)
    then
        insertLogical(new UnweightedConstraintOccurrence("multipleQueensDescendingDiagonal", $q1, $q2));
end

// ############################################################################
// Calculate score
// ############################################################################

rule "accumulateScore"
    when
        $occurrenceCount : Number() from accumulate(
            $unweightedConstraintOccurrence : UnweightedConstraintOccurrence(),
            count($unweightedConstraintOccurrence)
        )
    then
        scoreHolder.setScore(- $occurrenceCount.intValue());
end
