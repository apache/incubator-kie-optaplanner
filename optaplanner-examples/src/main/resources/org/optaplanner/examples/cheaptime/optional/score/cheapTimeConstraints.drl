package org.optaplanner.examples.cheaptime.optional.score;
    dialect "java"

import java.util.List;
import java.util.ArrayList;

import org.optaplanner.core.api.score.buildin.hardmediumsoftlong.HardMediumSoftLongScoreHolder;

import org.optaplanner.examples.cheaptime.domain.CheapTimeSolution;
import org.optaplanner.examples.cheaptime.domain.Machine;
import org.optaplanner.examples.cheaptime.domain.Period;
import org.optaplanner.examples.cheaptime.domain.Task;
import org.optaplanner.examples.cheaptime.domain.TaskAssignment;
import org.optaplanner.examples.cheaptime.score.CheapTimeCostCalculator;
import org.optaplanner.examples.cheaptime.optional.score.MachinePeriodPart;
import org.optaplanner.examples.cheaptime.optional.score.IdleCost;

global HardMediumSoftLongScoreHolder scoreHolder;

// Introduce MachinePeriodPart to score efficiently
rule "machinePeriodPart"
    salience 2
    when
        Period($period : index)
        $machine : Machine()
        $taskAssignmentList : ArrayList() from collect (
            TaskAssignment(
                machine == $machine,
                startPeriod <= $period,
                endPeriod > $period)
        )
    then
        insertLogical(new MachinePeriodPart($machine, $period, $taskAssignmentList));
end

// ############################################################################
// Hard constraints
// ############################################################################

// Start time limits: each task must start between its earliest start and latest start limit.
rule "Task starts too early"
    when
        $taskAssignment : TaskAssignment($task : task, $startPeriod : startPeriod < $task.startPeriodRangeFrom)
    then
        scoreHolder.addHardConstraintMatch(kcontext, $startPeriod - $task.getStartPeriodRangeFrom());
end

rule "Task starts too late"
    when
        $taskAssignment : TaskAssignment($task : task, $startPeriod : startPeriod >= $task.startPeriodRangeTo)
    then
        scoreHolder.addHardConstraintMatch(kcontext, $task.getStartPeriodRangeTo() - $startPeriod);
end

// Maximum capacity: the maximum capacity for each resource for each machine must not be exceeded.
rule "Maximum resource capacity" // resourceInShortTotal is calculated in MachinePeriodPart constructor
    when
        MachinePeriodPart($resourceInShortTotal : resourceInShortTotal < 0)
    then
        scoreHolder.addHardConstraintMatch(kcontext, $resourceInShortTotal);
end

// Startup and shutdown: each machine must be active in the periods during which it has assigned tasks.
//   Between tasks it is allowed to be idle to avoid startup and shutdown costs.

//     -> no rule is required because it's implemented in MachinePeriodPart

// ############################################################################
// Medium constraints
// ############################################################################

// Machine power cost: Each active or idle machine consumes power,
//   which infers a power cost (depending on the power price during that time).

// Machine startup and shutdown cost: Every time a machine starts up or shuts down, an extra cost is inflicted.

rule "calculateIdleCost"
    salience 1
    when
        MachinePeriodPart($machine : machine, $period : period, active == true)
        exists MachinePeriodPart(machine == $machine, period == $period - 1, active == false)
        exists MachinePeriodPart(machine == $machine, period < $period - 1, active == true)
        accumulate (
            MachinePeriodPart(machine == $machine, $activePeriod : period < $period - 1, active == true);
            $lastActivePeriod : max($activePeriod)
        )
        $idlePeriodPowerPriceList : ArrayList() from collect (
            Period(
                index > (Integer) $lastActivePeriod,
                index < $period)
        )
    then
        long idleCost = 0;
        for (Object obj : $idlePeriodPowerPriceList) {
            Period idlePeriod = (Period)obj;
            idleCost += CheapTimeCostCalculator.multiplyTwoMicros($machine.getPowerConsumptionMicros(), idlePeriod.getPowerPriceMicros());
        }
        insertLogical(new IdleCost($machine, $period, idleCost));
end

rule "Active machine power cost"
    when
        MachinePeriodPart($machine : machine, $period : period, active == true)
        Period(index == $period, $price: powerPriceMicros)
    then
        scoreHolder.addMediumConstraintMatch(kcontext,
            - CheapTimeCostCalculator.multiplyTwoMicros($machine.getPowerConsumptionMicros(), $price));
end

rule "Machine idle costs"
    when
        MachinePeriodPart($machine : machine, $period : period, active == true)
        IdleCost(machine == $machine, activePeriodAfterIdle == $period, $idleCost : cost)
    then
        if ($idleCost >= $machine.getSpinUpDownCostMicros()) {
            // Shutting down and restarting the machine is cheaper than keeping it idle.
            scoreHolder.addMediumConstraintMatch(kcontext, - $machine.getSpinUpDownCostMicros());
        } else {
            scoreHolder.addMediumConstraintMatch(kcontext, - $idleCost);
        }
end

rule "Active machine spin up and down cost"
    when
        MachinePeriodPart($machine : machine, $period : period, active == true)
        not MachinePeriodPart(machine == $machine, period < $period, active == true);
    then
        scoreHolder.addMediumConstraintMatch(kcontext, - $machine.getSpinUpDownCostMicros());
end

// Task power cost: Each task consumes power too, which infers a power cost (depending on the power price during its time).
rule "Task power cost"
    when
        Period($period: index, $price: powerPriceMicros)
        TaskAssignment(startPeriod <= $period, endPeriod > $period, $task : task)
    then
        scoreHolder.addMediumConstraintMatch(kcontext,
            - CheapTimeCostCalculator.multiplyTwoMicros($task.getPowerConsumptionMicros(), $price));
end

// ############################################################################
// Soft constraints
// ############################################################################

// Start early: prefer starting a task sooner rather than later.
rule "Prefer early task start"
    when
        TaskAssignment($startPeriod : startPeriod != null)
    then
        scoreHolder.addSoftConstraintMatch(kcontext, - $startPeriod);
end
