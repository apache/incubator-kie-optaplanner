import org.jenkinsci.plugins.workflow.libs.Library
@Library('jenkins-pipeline-shared-libraries')_

import org.kie.jenkins.MavenCommand
import org.kie.jenkins.MavenStagingHelper

deployProperties = [:]

optaplannerRepository = 'optaplanner'
quickstartsRepository = 'optaplanner-quickstarts'
vehicleRoutingRepository = 'optaweb-vehicle-routing'
employeeRosteringRepository = 'optaweb-employee-rostering'

pipeline {
    agent {
        label 'kie-rhel7 && kie-mem16g && !master'
    }

    tools {
        maven 'kie-maven-3.8.1'
        jdk 'kie-jdk11'
    }

    options {
        timestamps()
        timeout(time: 120, unit: 'MINUTES')
        disableConcurrentBuilds(abortPrevious: true)
    }

    // parameters {
    // For parameters, check into .jenkins/dsl/jobs.groovy file
    // }

    environment {
        // Some generated env is also defined into .jenkins/dsl/jobs.groovy file

        KOGITO_CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")

        MAVEN_OPTS = '-Xms1024m -Xmx4g'

        BOT_BRANCH_HASH = "${util.generateHash(10)}"

        // Maven configuration
        MAVEN_DEPLOY_LOCAL_DIR = "${WORKSPACE}/maven_deploy_dir"
    }

    stages {
        stage('Initialize') {
            steps {
                script {
                    cleanWs()

                    if (params.DISPLAY_NAME) {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    if (isRelease() || isCreatePr()) {
                        // Verify version is set
                        assert getProjectVersion()
                        assert getDroolsVersion()

                        if (isRelease()) {
                            // Verify if on right release branch
                            assert getBuildBranch() == util.getReleaseBranchFromVersion(getProjectVersion())
                        }
                    }
                }
            }
            post {
                success {
                    script {
                        setDeployPropertyIfNeeded('git.branch', getBuildBranch())
                        setDeployPropertyIfNeeded('git.branchQuickstarts', getQuickStartsBranch())
                        setDeployPropertyIfNeeded('git.author', getGitAuthor())
                        setDeployPropertyIfNeeded('project.version', getProjectVersion())
                        setDeployPropertyIfNeeded('release', isRelease())
                        setDeployPropertyIfNeeded('drools.version', getDroolsVersion())
                    }
                }
            }
        }

        stage('Clone repositories') {
            steps {
                checkoutRepo(optaplannerRepository)
                checkoutQuickstarts()
                checkoutRepo(vehicleRoutingRepository)
                checkoutRepo(employeeRosteringRepository)
            }
        }

        stage('Prepare for PR') {
            when {
                expression { return isRelease() || isCreatePr()  }
            }
            steps {
                prepareForPR(optaplannerRepository)
                prepareForPR(vehicleRoutingRepository)
                prepareForPR(employeeRosteringRepository)
                prepareForPR(quickstartsRepository)
            }
        }

        stage('Update project version') {
            when {
                expression { return getProjectVersion() != '' }
            }
            steps {
                script {
                    maven.mvnSetVersionProperty(getOptaplannerMavenCommand(), 'version.org.drools', getDroolsVersion())
                    maven.mvnVersionsSet(getOptaplannerMavenCommand(), getProjectVersion(), !isRelease())

                    mavenCleanInstallOptaPlannerParents()

                    maven.mvnVersionsUpdateParentAndChildModules(getOptawebVehicleRoutingMavenCommand(), getProjectVersion(), !isRelease())
                    maven.mvnVersionsUpdateParentAndChildModules(getOptawebEmployeeRosteringMavenCommand(), getProjectVersion(), !isRelease())

                    updateQuickstartsVersions()
                }
            }
        }
        stage('Build OptaPlanner') {
            steps {
                script {
                    getOptaplannerMavenCommand().withProperty('maven.test.failure.ignore', true).skipTests(params.SKIP_TESTS).run('clean install')
                    if (isRelease()) {
                        updateAntoraYaml(optaplannerRepository)
                    }
                }
            }
            post {
                always {
                    script {
                        archiveJUnitTestResults()
                        util.archiveConsoleLog()
                    }
                }
            }
        }

        stage('Build Quickstarts') {
            steps {
                script {
                    getOptaplannerQuickstartsMavenCommand().withProperty('maven.test.failure.ignore', true).skipTests(params.SKIP_TESTS).run('clean install')
                }
            }
            post {
                always {
                    script {
                        archiveJUnitTestResults()
                        util.archiveConsoleLog()
                    }
                }
            }
        }

        stage('Build Vehicle Routing') {
            steps {
                script {
                    buildOptaweb(getOptawebVehicleRoutingMavenCommand())
                }
            }
            post {
                always {
                    script {
                        archiveJUnitTestResults()
                        archiveCypressArtifacts()
                        util.archiveConsoleLog()
                    }
                }
            }
        }

        stage('Build Employee Rostering') {
            steps {
                script {
                    buildOptaweb(getOptawebEmployeeRosteringMavenCommand())
                }
            }
            post {
                always {
                    script {
                        archiveJUnitTestResults()
                        archiveCypressArtifacts()
                        util.archiveConsoleLog()
                    }
                }
            }
        }

        stage('Deploy to repository') {
            when {
                expression { return !shouldStageArtifacts() && !isSpecificArtifactsUpload() && shouldDeployToRepository() }
            }
            steps {
                script {
                    runMavenDeploy(getOptaplannerMavenCommand())
                    runMavenDeploy(getOptaplannerQuickstartsMavenCommand().withOptions(['-pl', ':optaplanner-distribution']))
                    runMavenDeploy(getOptawebVehicleRoutingMavenCommand())
                    runMavenDeploy(getOptawebEmployeeRosteringMavenCommand())
                }
            }
        }
        stage('Deploy locally') {
            when {
                expression { return shouldStageArtifacts() || isSpecificArtifactsUpload() }
            }
            steps {
                script {
                    runMavenDeploy(getOptaplannerMavenCommand(), optaplannerRepository)
                    runMavenDeploy(getOptawebVehicleRoutingMavenCommand(), vehicleRoutingRepository)
                    runMavenDeploy(getOptawebEmployeeRosteringMavenCommand(), employeeRosteringRepository)
                }
            }
        }
        stage('Upload artifacts to given repository') {
            when {
                expression { return isSpecificArtifactsUpload() }
            }
            steps {
                script {
                    // Deploy to specific repository with credentials
                    maven.uploadLocalArtifacts(env.MAVEN_REPO_CREDS_ID, getLocalDeploymentFolder(optaplannerRepository), getMavenRepoZipUrl())
                    maven.uploadLocalArtifacts(env.MAVEN_REPO_CREDS_ID, getLocalDeploymentFolder(vehicleRoutingRepository), getMavenRepoZipUrl())
                    maven.uploadLocalArtifacts(env.MAVEN_REPO_CREDS_ID, getLocalDeploymentFolder(employeeRosteringRepository), getMavenRepoZipUrl())
                }
            }
        }
        stage('Stage artifacts') {
            when {
                expression { return shouldStageArtifacts() }
            }
            steps {
                script {
                    // Stage release artifacts
                    runMavenStage(getOptaplannerMavenCommand(), optaplannerRepository)
                    runMavenStage(getOptawebVehicleRoutingMavenCommand(), vehicleRoutingRepository)
                    runMavenStage(getOptawebEmployeeRosteringMavenCommand(), employeeRosteringRepository)
                }
            }
        }

        stage('Create PRs') {
            when {
                expression { return isRelease() || isCreatePr()  }
            }
            steps {
                commitAndCreatePR(optaplannerRepository, getBuildBranch())
                commitAndCreatePRIgnoringNpmRegistry(vehicleRoutingRepository, getBuildBranch())
                commitAndCreatePRIgnoringNpmRegistry(employeeRosteringRepository, getBuildBranch())
                commitAndCreatePR(quickstartsRepository, getQuickStartsBranch())
            }
            post {
                success {
                    script {
                        setDeployPropertyIfNeeded("${optaplannerRepository}.pr.source.uri", "https://github.com/${getBotAuthor()}/${optaplannerRepository}")
                        setDeployPropertyIfNeeded("${optaplannerRepository}.pr.source.ref", getBotBranch())
                        setDeployPropertyIfNeeded("${optaplannerRepository}.pr.target.uri", "https://github.com/${getGitAuthor()}/${optaplannerRepository}")
                        setDeployPropertyIfNeeded("${optaplannerRepository}.pr.target.ref", getBuildBranch())

                        setDeployPropertyIfNeeded("${vehicleRoutingRepository}.pr.source.uri", "https://github.com/${getBotAuthor()}/${vehicleRoutingRepository}")
                        setDeployPropertyIfNeeded("${vehicleRoutingRepository}.pr.source.ref", getBotBranch())
                        setDeployPropertyIfNeeded("${vehicleRoutingRepository}.pr.target.uri", "https://github.com/${getGitAuthor()}/${vehicleRoutingRepository}")
                        setDeployPropertyIfNeeded("${vehicleRoutingRepository}.pr.target.ref", getBuildBranch())

                        setDeployPropertyIfNeeded("${employeeRosteringRepository}.pr.source.uri", "https://github.com/${getBotAuthor()}/${employeeRosteringRepository}")
                        setDeployPropertyIfNeeded("${employeeRosteringRepository}.pr.source.ref", getBotBranch())
                        setDeployPropertyIfNeeded("${employeeRosteringRepository}.pr.target.uri", "https://github.com/${getGitAuthor()}/${employeeRosteringRepository}")
                        setDeployPropertyIfNeeded("${employeeRosteringRepository}.pr.target.ref", getBuildBranch())
                    }
                }
            }
        }
    }
    post {
        always {
            script {
                def propertiesStr = deployProperties.collect { entry ->  "${entry.key}=${entry.value}" }.join('\n')
                writeFile(text: propertiesStr, file: env.PROPERTIES_FILE_NAME)
                archiveArtifacts(artifacts: env.PROPERTIES_FILE_NAME)
            }
        }
        unsuccessful {
            sendNotification()
        }
        cleanup {
            script {
                // Clean also docker in case of usage of testcontainers lib
                util.cleanNode('docker')
            }
        }
    }
}

void sendNotification() {
    if (params.SEND_NOTIFICATION) {
        mailer.sendMarkdownTestSummaryNotification('Deploy', "[${getBuildBranch()}] Optaplanner", [env.KOGITO_CI_EMAIL_TO], "cc @*optaplanner-team*")
    } else {
        echo 'No notification sent per configuration'
    }
}

void buildOptaweb(MavenCommand optawebCommand) {
    MavenCommand buildCommand = optawebCommand
    if (!skipIntegrationTests()) {
        buildCommand = buildCommand.withProfiles(getIntegrationTestProfiles())
    }
    buildCommand.withProperty('maven.test.failure.ignore', true).skipTests(params.SKIP_TESTS).run('clean install')
}

void archiveCypressArtifacts() {
    if (!skipIntegrationTests()) {
        archiveArtifacts(artifacts: '**/cypress/screenshots/**,**/cypress/videos/**', fingerprint: false, allowEmptyArchive: true)
    }
}

List getIntegrationTestProfiles() {
    return params.SKIP_INTEGRATION_TESTS ? [] : ['integration-tests']
}

void updateQuickstartsVersions() {
    maven.mvnSetVersionProperty(getOptaplannerQuickstartsMavenCommand(), 'version.org.optaplanner', getProjectVersion())
    maven.mvnVersionsUpdateParentAndChildModules(getOptaplannerQuickstartsMavenCommand(), getProjectVersion(), !isRelease())
    gradleVersionsUpdate(quickstartsRepository, getProjectVersion())

    if (isRelease()) {
        dir(quickstartsRepository) {
            assert !sh (script:
                    'grep -Rn "SNAPSHOT" --include={pom.xml,build.gradle} | ' +
                    'grep -v -e "1.0-SNAPSHOT" | ' +
                    'cat', returnStdout: true)
        }
    }
    if (isCreatePr()) {
        dir(quickstartsRepository) {
            assert !sh (script:
                    'grep -Rn "SNAPSHOT" --include={pom.xml,build.gradle} | ' +
                    'grep -v -e "${getProjectVersion()}" | ' +
                    'cat', returnStdout: true)
        }
    }
}

void gradleVersionsUpdate(String repo, String newVersion) {
    dir(repo) {
        sh "find . -name build.gradle -exec sed -i -E 's/def optaplannerVersion = \"[^\"\\s]+\"/def optaplannerVersion = \"${newVersion}\"/' {} \\;"
    }
}

void archiveJUnitTestResults() {
    if (!skipUnitTests()) {
        junit testResults: '**/target/surefire-reports/**/*.xml, **/target/failsafe-reports/**/*.xml', allowEmptyResults: true
    }
}

void checkoutRepo(String repo, String dirName=repo) {
    dir(dirName) {
        deleteDir()
        if (params.PR_TARGET_BRANCH) {
            githubscm.checkoutIfExists(repo, getGitAuthor(), getBuildBranch(), 'kiegroup', getFallbackBranch(repo), true)
        } else {
            checkout(githubscm.resolveRepository(repo, getGitAuthor(), getBuildBranch(), false))
        }
    }
}

void checkoutQuickstarts(String dirName=quickstartsRepository) {
    dir(dirName) {
        deleteDir()
        if (params.PR_TARGET_BRANCH) {
            githubscm.checkoutIfExists(quickstartsRepository, getGitAuthor(), getBuildBranch(), 'kiegroup', getQuickStartsBranch(), true)
        } else {
            checkout(githubscm.resolveRepository(quickstartsRepository, getGitAuthor(), getQuickStartsBranch(), false))
        }
    }
}

String getFallbackBranch(String repo) {
    def repositoryScm = githubscm.getRepositoryScm(repo, 'kiegroup', params.PR_TARGET_BRANCH)
    return repositoryScm ? params.PR_TARGET_BRANCH : 'main'
}

void prepareForPR(String repo) {
    dir(repo) {
        githubscm.forkRepo(getBotAuthorCredsID())
        githubscm.createBranch(getBotBranch())
    }
}

void commitAndCreatePR(String repo, String buildBranch) {
    dir(repo) {
        def commitMsg = "[${buildBranch}] Update project version to ${getProjectVersion()}"
        def prBody = "Generated by build ${BUILD_TAG}: ${BUILD_URL}."
        if (isRelease()) {
            prBody += '\nPlease do not merge, it should be merged automatically after testing.'
        } else {
            prBody += '\nPlease review and merge.'
        }

        githubscm.commitChanges(commitMsg, {
            githubscm.findAndStageNotIgnoredFiles('pom.xml')
            githubscm.findAndStageNotIgnoredFiles('build.gradle')
            githubscm.findAndStageNotIgnoredFiles('antora.yml')
        })
        githubscm.pushObject('origin', getBotBranch(), getBotAuthorCredsID())
        deployProperties["${repo}.pr.link"] = isRelease() ?
                                                githubscm.createPRWithLabels(commitMsg, prBody, buildBranch, ['DO_NOT_MERGE'] as String[], getBotAuthorCredsID()) :
                                                githubscm.createPR(commitMsg, prBody, buildBranch, getBotAuthorCredsID())
    }
}

void commitAndCreatePRIgnoringNpmRegistry(String repo, String buildBranch) {
    dir(repo) {
        sh 'sed \'s;repository.engineering.redhat.com/nexus/repository/;;\' -i */package-lock.json'
        sh 'git add */package-lock.json'
    }
    commitAndCreatePR(repo, buildBranch)
}

MavenCommand getMavenDefaultCommand() {
    MavenCommand mvnCmd = new MavenCommand(this, ['-fae', '-ntp']).withSettingsXmlId(env.MAVEN_SETTINGS_CONFIG_FILE_ID)
    if (env.MAVEN_DEPENDENCIES_REPOSITORY) {
        mvnCmd.withDependencyRepositoryInSettings('deps-repo', env.MAVEN_DEPENDENCIES_REPOSITORY)
    }
    return mvnCmd
}

MavenCommand getOptaplannerMavenCommand() {
    return getMavenDefaultCommand().inDirectory(optaplannerRepository).withProperty('full')
}

MavenCommand getOptaplannerQuickstartsMavenCommand() {
    return getMavenDefaultCommand().inDirectory(quickstartsRepository).withProperty('full')
}

MavenCommand getOptawebVehicleRoutingMavenCommand() {
    return getMavenDefaultCommand().inDirectory(vehicleRoutingRepository)
}

MavenCommand getOptawebEmployeeRosteringMavenCommand() {
    return getMavenDefaultCommand().inDirectory(employeeRosteringRepository)
}

/**
 * Builds the parent modules and the BOM so that project depending on these artifacts can resolve.
 */
void mavenCleanInstallOptaPlannerParents() {
    getOptaplannerMavenCommand()
        .skipTests(true)
        .withOptions(['-U', '-pl org.optaplanner:optaplanner-build-parent,org.optaplanner:optaplanner-bom', '-am'])
        .run('clean install')
}

void runMavenDeploy(MavenCommand mvnCmd, String localDeploymentId = '') {
    mvnCmd = mvnCmd.clone()

    if (localDeploymentId) {
        mvnCmd.withLocalDeployFolder(getLocalDeploymentFolder(localDeploymentId))
    } else if (env.MAVEN_DEPLOY_REPOSITORY) {
        mvnCmd.withDeployRepository(env.MAVEN_DEPLOY_REPOSITORY)
    }

    mvnCmd.skipTests(true).run('clean deploy')
}

String getMavenRepoZipUrl() {
    return "${params.MAVEN_DEPLOY_REPOSITORY.replaceAll('/content/', '/service/local/').replaceFirst('/*$', '')}/content-compressed"
}

void runMavenStage(MavenCommand mvnCmd, String localDeployId) {
    MavenStagingHelper stagingHelper = getStagingHelper(mvnCmd)
    deployProperties.putAll(stagingHelper.stageLocalArtifacts(env.NEXUS_STAGING_PROFILE_ID, getLocalDeploymentFolder(localDeployId)))
    stagingHelper.promoteStagingRepository(env.NEXUS_BUILD_PROMOTION_PROFILE_ID)
}

MavenStagingHelper getStagingHelper(MavenCommand mvnCmd) {
    return new MavenStagingHelper(this, mvnCmd)
                .withNexusReleaseUrl(env.NEXUS_RELEASE_URL)
                .withNexusReleaseRepositoryId(env.NEXUS_RELEASE_REPOSITORY_ID)
}

String getLocalDeploymentFolder(String localDeployId) {
    return "${env.MAVEN_DEPLOY_LOCAL_DIR}/${localDeployId}"
}

// Getters and Setters of params/properties

boolean isSpecificArtifactsUpload() {
    return env.MAVEN_DEPLOY_REPOSITORY && env.MAVEN_REPO_CREDS_ID
}

boolean shouldStageArtifacts() {
    return !isSpecificArtifactsUpload() && isRelease() && !env.MAVEN_DEPLOY_REPOSITORY
}

boolean shouldDeployToRepository() {
    return env.MAVEN_DEPLOY_REPOSITORY || isNotTestingBuild()
}

boolean isNotTestingBuild() {
    return getGitAuthor() == 'kiegroup'
}

boolean isRelease() {
    return env.RELEASE ? env.RELEASE.toBoolean() : false
}

boolean isCreatePr() {
    return params.CREATE_PR
}

boolean skipUnitTests() {
    return params.SKIP_TESTS
}

boolean skipIntegrationTests() {
    return params.SKIP_INTEGRATION_TESTS
}

String getGitAuthor() {
    // GIT_AUTHOR can be env or param
    return "${GIT_AUTHOR}"
}

String getBuildBranch() {
    return params.BUILD_BRANCH_NAME
}

String getDroolsVersion() {
    return params.DROOLS_VERSION
}

String getProjectVersion() {
    return params.PROJECT_VERSION
}

String getBotBranch() {
    return "${getProjectVersion()}-${env.BOT_BRANCH_HASH}"
}

String getBotAuthor() {
    return env.GIT_AUTHOR_BOT
}

String getBotAuthorCredsID() {
    return env.BOT_CREDENTIALS_ID
}

void setDeployPropertyIfNeeded(String key, def value) {
    if (value != null && value != '') {
        deployProperties[key] = value
    }
}

String getQuickStartsBranch() {
    return params.QUICKSTARTS_BUILD_BRANCH_NAME
}

void updateAntoraYaml(String directory) {
    if (isNotTestingBuild()) {
        echo "updateAntoraYaml for ${directory}"
        dir(directory) {
            sh './build/release/update_antora_yml.sh'
        }
    } else {
        echo 'No updateAntoraYaml due to testing build'
    }
}
