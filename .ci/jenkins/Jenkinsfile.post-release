import org.jenkinsci.plugins.workflow.libs.Library

@Library('jenkins-pipeline-shared-libraries')_

import org.kie.jenkins.MavenCommand

String optaplannerRepository = 'optaplanner'
String quickstartsRepository = 'optaplanner-quickstarts'
String stableBranchName = 'stable'
String kieBenchmarksRepo = 'kie-benchmarks'

pipeline {
    agent {
        label 'kie-rhel7 && !master'
    }

    tools {
        maven env.BUILD_MAVEN_TOOL
        jdk env.BUILD_JDK_TOOL
    }

    options {
        timestamps()
        timeout(time: 120, unit: 'MINUTES')
        disableConcurrentBuilds(abortPrevious: true)
    }

    // parameters {
    // For parameters, check into .jenkins/dsl/jobs.groovy file
    // }

    environment {
        // Some generated env is also defined into .jenkins/dsl/jobs.groovy file

        OPTAPLANNER_CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")

        BOT_BRANCH_HASH = "${util.generateHash(10)}"
    }

    stages {
        stage('Initialization') {
            steps {
                script {
                    cleanWs()

                    if (params.DISPLAY_NAME) {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    // Verify version is set and if on right release branch
                    assert getProjectVersion()

                    assert getBuildBranch() == util.getReleaseBranchFromVersion(getProjectVersion())

                    installGithubCLI()
                }
            }
        }

        stage('Reset Quickstarts stable branch') {
            steps {
                script {
                    dir(quickstartsRepository) {
                        checkoutTag(quickstartsRepository, getGitTag(), stableBranchName)
                        removeJbossNexusFromMavenAndGradle()
                        commitAndForcePushProtectedBranch(quickstartsRepository, stableBranchName)
                    }
                }
            }
        }

        stage('Upload OptaPlanner distribution from Quickstarts') {
            steps {
                script {
                    dir(optaplannerRepository) {
                        checkoutTag(optaplannerRepository, getProjectVersion(), stableBranchName)
                        getMavenCommand().withProperty('quickly').withProperty('full').run('clean install')
                    }

                    getMavenCommand().inDirectory(quickstartsRepository).skipTests(true).withProperty('full').run('clean install')
                    uploadDistribution(quickstartsRepository)
                }
            }
        }

        stage('Update OptaPlanner website') {
            steps {
                script {
                    final String websiteRepository = 'optaplanner-website'
                    String prLink = null
                    String prBranchName = "${getProjectVersion().toLowerCase()}-${env.BOT_BRANCH_HASH}"
                    dir("$websiteRepository-bot") {
                        checkoutRepo(websiteRepository, 'main') // there is no other branch
                        githubscm.createBranch(prBranchName)

                        // Update versions in links on the website and in the docs.
                        sh "./build/update-versions.sh ${getProjectVersion()} ${getNextMinorSnapshotVersion(getProjectVersion())}"

                        // Update the XSDs. OptaPlanner must be cloned and build with the full profile before.
                        String optaplannerRoot = "${WORKSPACE}/${optaplannerRepository}"
                        String optaplannerWebsiteModule = 'optaplanner-website-root'
                        String optaplannerWebsiteXsd = "${optaplannerWebsiteModule}/content/xsd"
                        String optaplannerWebsiteDocs = 'optaplanner-website-docs'
                        sh "cp ${optaplannerRoot}/core/optaplanner-core-impl/target/classes/solver.xsd ${optaplannerWebsiteXsd}/solver/solver-8.xsd"
                        sh "cp ${optaplannerRoot}/optaplanner-benchmark/target/classes/benchmark.xsd ${optaplannerWebsiteXsd}/benchmark/benchmark-8.xsd"

                        // Add changed files, commit, open and merge PR
                        prLink = commitAndCreatePR("Release OptaPlanner ${getProjectVersion()}",
                                { sh "git add $optaplannerWebsiteXsd/\\*.xsd $optaplannerWebsiteModule/data/pom.yml $optaplannerWebsiteDocs/antora-playbook.yml" },
                                prBranchName, 'main')
                    }
                    dir(websiteRepository) {
                        checkoutRepo(websiteRepository, 'main')
                        mergeAndPush(prLink, 'main')
                        githubscm.removeRemoteBranch('origin', prBranchName, getGitAuthorCredsID())
                    }
                }
            }
        }
        stage('update version.org.optaplanner property in kie-benchmarks') {
            steps {
                script {
                    String prLink = null
                    String commitMsg = 'bumped up kie-version to next SNAPSHOT'
                    String localBranch = 'kie-benchmarks-update'
                    String targetBranch = 'main'
                    dir(kieBenchmarksRepo){
                        checkoutRepo(kieBenchmarksRepo, targetBranch)
                        githubscm.setUserConfigFromCreds()
                        githubscm.createBranch(localBranch)
                        // update version.org.optaplanner property in kie-benchmarks
                        maven.mvnSetVersionProperty('version.org.optaplanner', getNextMinorSnapshotVersion(getProjectVersion()))
                        // Add changed files, commit, open and merge PR
                        prLink = commitAndCreatePR(commitMsg, { sh "git add --all" }, localBranch, targetBranch)
                        sh "git checkout ${targetBranch}"
                        mergeAndPush(prLink, targetBranch)
                        githubscm.removeRemoteBranch('origin', localBranch, getGitAuthorCredsID())
                    }
                }
            }
        }
    }
    post {
        unsuccessful {
            sendNotification()
        }
        cleanup {
            script {
                // Clean also docker in case of usage of testcontainers lib
                util.cleanNode('docker')
            }
        }
    }
}

void sendNotification() {
    mailer.sendMarkdownTestSummaryNotification('Post-release', "[${getBuildBranch()}] Optaplanner", [env.OPTAPLANNER_CI_EMAIL_TO])
}

//////////////////////////////////////////////////////////////////////////////
// Getter / Setter
//////////////////////////////////////////////////////////////////////////////

boolean isNotTestingBuild() {
    return getGitAuthor() == 'kiegroup'
}

String getProjectVersion() {
    return params.PROJECT_VERSION
}

String getNextMicroSnapshotVersion(String currentVersion) {
    return util.getNextVersion(currentVersion, 'micro')
}

String getNextMinorSnapshotVersion(String currentVersion) {
    return util.getNextVersion(currentVersion, 'minor')
}

String getGitTag() {
    return params.GIT_TAG
}

String getBuildBranch() {
    return params.BUILD_BRANCH_NAME
}

String getGitAuthor() {
    return env.GIT_AUTHOR
}

String getGitAuthorCredsID() {
    return env.AUTHOR_CREDS_ID
}

String getSnapshotBranch() {
    return "${getNextMicroSnapshotVersion(getProjectVersion()).toLowerCase()}-${env.BOT_BRANCH_HASH}"
}

//////////////////////////////////////////////////////////////////////////////
// Git
//////////////////////////////////////////////////////////////////////////////

void checkoutRepo(String repo, String branch) {
    deleteDir()
    checkout(githubscm.resolveRepository(repo, getGitAuthor(), branch, false))
    // need to manually checkout branch since on a detached branch after checkout command
    sh "git checkout ${branch}"
}

void checkoutTag(String repo, String tagName, String localBranchName = tagName) {
    deleteDir()
    checkout(githubscm.resolveRepository(repo, getGitAuthor(), getBuildBranch(), false))
    // Need to manually checkout branch since we are in 'detached HEAD' state after the git checkout command.
    sh "git checkout tags/${tagName} -b ${localBranchName}"
}

void mergeAndPush(String prLink, String targetBranch) {
    if (prLink != '') {
        githubscm.mergePR(prLink, getGitAuthorCredsID())
        githubscm.pushObject('origin', targetBranch, getGitAuthorCredsID())
    }
}

String commitAndCreatePR(String commitMsg, Closure precommit, String localBranch, String targetBranch) {
    def prBody = "Generated by build ${BUILD_TAG}: ${BUILD_URL}"

    githubscm.commitChanges(commitMsg, precommit)
    githubscm.pushObject('origin', localBranch, getGitAuthorCredsID())
    return githubscm.createPR(commitMsg, prBody, targetBranch, getGitAuthorCredsID())
}

void commitAndForcePushProtectedBranch(String repo, String branch) {
    githubscm.commitChanges("[${getBuildBranch()}] Update ${branch} to ${getProjectVersion()}", {
        githubscm.findAndStageNotIgnoredFiles('pom.xml')
        githubscm.findAndStageNotIgnoredFiles('build.gradle')
    })
    try {
        forcePushProtectedBranch(repo, branch)
    }
    catch (exception) {
        println "[ERROR] Force push branch: ${branch} from repo : ${repo} failed with exception: ${exception}"
        currentBuild.result = 'UNSTABLE'
    }
}

void installGithubCLI() {
    sh """
    wget https://github.com/cli/cli/releases/download/v${env.GITHUB_CLI_VERSION}/gh_${env.GITHUB_CLI_VERSION}_linux_amd64.tar.gz
    tar xzf gh_${env.GITHUB_CLI_VERSION}_linux_amd64.tar.gz
    mv gh_${env.GITHUB_CLI_VERSION}_linux_amd64/bin/gh .
    rm -r gh_${env.GITHUB_CLI_VERSION}_linux_amd64*
    """
}

void uploadDistribution(String directory) {
    if (isNotTestingBuild()) {
        echo "uploadDistribution for ${directory}"
        dir(directory) {
            withCredentials(bindings: [sshUserPrivateKey(credentialsId: 'optaplanner-filemgmt',
                    keyFileVariable: 'SSH_KEY_JBOSS_FILEMGMT')]) {
                // For testing, simulate connection via SSH:
                // sh "ssh -i $SSH_KEY_JBOSS_FILEMGMT -oKexAlgorithms=+diffie-hellman-group1-sha1 optaplanner@filemgmt.jboss.org"
                sh "./build/release/upload_distribution.sh ${getProjectVersion()} $SSH_KEY_JBOSS_FILEMGMT"
            }
        }
    } else {
        echo 'No uploadDistribution due to testing build'
    }
}

MavenCommand getMavenCommand() {
    mvnCmd = new MavenCommand(this, ['-fae'])
                    .withSettingsXmlId(env.MAVEN_SETTINGS_CONFIG_FILE_ID)
    if (env.MAVEN_DEPENDENCIES_REPOSITORY) {
        mvnCmd.withDependencyRepositoryInSettings('deps-repo', env.MAVEN_DEPENDENCIES_REPOSITORY)
    }
    return mvnCmd
}

void removeJbossNexusFromMavenAndGradle() {
    sh "find . -name pom.xml -print0 | xargs -0 sed -i \':a;N;\$!ba;s/\\n *<repositories>.*<\\/repositories>//g\'"
    sh "find . -name build.gradle -print0 | xargs -0 sed -i \':a;N;\$!ba;s/\\n *maven {[^{}]*mavenContent " +
            "{[^{}]*snapshotsOnly[^{}]*}[^{}]*}//g\'"

    assert !sh (script:
            'grep -Rn "repository.jboss.org" --include={pom.xml,build.gradle} | ' +
            'cat', returnStdout: true)
}

String getGhCredsID() {
    return env.GITHUB_TOKEN_CREDS_ID
}

//maps git branch protection response json into update request body to rewrite branch protection
String getProtectionMapScript() {
    return "if .restrictions == null then . + {\"restrictions_used\":null}  " + //upd user set restrictions if not empty
            "else . + {\"restrictions_used\":{\"users\":[.restrictions.users[].login], \"team\":[.restrictions.users[].slug]}} end |" +
            ' {' +
                "\"required_status_checks\": .required_status_checks," +
                "\"required_pull_request_reviews\": .required_pull_request_reviews," +
                "\"enforce_admins\":(.enforce_admins.enabled)," +
                " \"restrictions\":.restrictions_used" +
            '}'
}

def enableForcePushes(String repo, String protectedBranch) {
    if (isBranchProtected(repo, protectedBranch)) {
        setAllowForcePushes(repo, protectedBranch, 'true')
    }
}

def disableForcePushes(String repo, String protectedBranch) {
    if (isBranchProtected(repo, protectedBranch)) {
        setAllowForcePushes(repo, protectedBranch, 'false')
    }
}

boolean isBranchProtected(String repo, String protectedBranch, String ghPath = '../gh') {
    assertGithubCLI(ghPath)
    boolean isProtected = true // Suppose it is protected by default
    withCredentials([string(credentialsId: getGhCredsID(), variable: 'GITHUB_TOKEN')]) {
        // get current branch protection
        int status = sh (script: "${ghPath} api 'repos/${getGitAuthor()}/${repo}/branches/${protectedBranch}/protection'", returnStatus: true)
        isProtected = status == 0
    }
    return isProtected
}

def setAllowForcePushes(String repo, String protectedBranch, String enabled, String ghPath = '../gh') {
    assertGithubCLI(ghPath)
    //Use separate admin token credentials
    withCredentials([string(credentialsId: getGhCredsID(), variable: 'GITHUB_TOKEN')]) {
        //get current branch protection and remove allow force push
        sh "${ghPath} api 'repos/${getGitAuthor()}/${repo}/branches/${protectedBranch}/protection' | " +
                "jq 'del(.allow_force_pushes)' > protectionBefore.json"

        //create new json based on current protection mapped as parameters
        sh "jq \"${getProtectionMapScript()}\" protectionBefore.json | " +
                "jq \". + {\"allow_force_pushes\":${enabled}}\" > protectionParameters.json"

        //update protection on git
        def allowForcePushEnabled = sh(script:
                "${ghPath} api -XPUT 'repos/${getGitAuthor()}/${repo}/branches/${protectedBranch}/protection' " +
                        '--input protectionParameters.json |' +
                        " jq '.allow_force_pushes.enabled'", returnStdout: true).trim()
        assert allowForcePushEnabled == enabled

        //check that protection didn't changed except for allow_force_pushes
        sh "${ghPath} api 'repos/${getGitAuthor()}/${repo}/branches/${protectedBranch}/protection' | " +
                "jq 'del(.allow_force_pushes)' > protectionAfter.json"
        def differences = sh(script: 'diff protectionBefore.json protectionAfter.json | cat', returnStdout: true)
        if (differences) {
            error 'Protection settings lost' +
                    '\nBefore: ' +
                    "\n${readFile 'protectionBefore.json'} " +
                    '\nAfter: ' +
                    "\n${readFile 'protectionAfter.json'} " +
                    '\nDifferences: ' +
                    "\n${differences} " +
                    '\nProtection parameters: ' +
                    "\n${readFile 'protectionParameters.json'} " +
                    'Please rollback to Before state and update getProtectionMapScript'
        }
        //cleanup workspace
        sh 'rm -f protectionParameters.json protectionBefore.json protectionAfter.json'
    }
}

def forcePushProtectedBranch(String repo, String protectedBranch) {
    enableForcePushes(repo, protectedBranch)
    withCredentials([usernamePassword(credentialsId: getGitAuthorCredsID(), usernameVariable: 'GIT_USERNAME', passwordVariable: 'GIT_PASSWORD')]) {
        // Please leave the double-quote here. They are mandatory for the shell command to work correctly.
        sh """
            git config --local credential.helper \"!f() { echo username=\\$GIT_USERNAME; echo password=\\$GIT_PASSWORD; }; f\"
            git push origin ${protectedBranch} --force
        """
    }
    disableForcePushes(repo, protectedBranch)
}

void assertGithubCLI(String ghPath) {
    if (fileExists(ghPath)) {
        echo "[INFO] gh found at $ghPath"
    }
    else {
        error "gh not found at $ghPath"
    }
}
