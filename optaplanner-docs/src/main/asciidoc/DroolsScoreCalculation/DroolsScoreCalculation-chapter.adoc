[[droolsScoreCalculation]]
= Drools Score Calculation
:doctype: book
:imagesdir: ..
:sectnums:
:toc: left
:icons: font
:experimental:


[[droolsScoreCalculationOverview]]
== Overview

Implement your score calculation using the Drools rule engine.
Every score constraint is written as one or more score rules.

* Advantages:
** Incremental score calculation for free
*** Because most DRL syntax uses forward chaining, it does incremental calculation without any extra code
** Score constraints are isolated as separate rules
*** Easy to add or edit existing score rules
** Flexibility to augment your score constraints by
*** Defining them in decision tables
**** Excel (XLS) spreadsheet
**** KIE Workbench WebUI
*** Translate them into natural language with DSL
*** Store and release in the KIE Workbench repository
** Performance optimizations in future versions for free
*** In every release, the Drools rule engine tends to become faster
* Disadvantages:
** DRL learning curve
** Usage of DRL
*** Polyglot fear can prohibit the use of a new language such as DRL in some organizations


[[droolsScoreRulesConfiguration]]
== Drools Score Rules Configuration

There are several ways to define where your score rules live.


[[droolsScoreCalculationScoreDrl]]
=== A scoreDrl Resource on the Classpath

This is the easy way.
The score rules live in a DRL file which is provided as a classpath resource.
Just add the score rules DRL file in the solver configuration as a `<scoreDrl>` element:

[source,xml,options="nowrap"]
----
  <scoreDirectorFactory>
    <scoreDrl>org/optaplanner/examples/nqueens/solver/nQueensScoreRules.drl</scoreDrl>
  </scoreDirectorFactory>
----

In a typical project (following the Maven directory structure), that DRL file would be located at `$PROJECT_DIR/src/main/resources/org/optaplanner/examples/nqueens/solver/nQueensScoreRules.drl` (even for a war project).

[NOTE]
====
The `<scoreDrl>` element expects a classpath resource, as defined by ``ClassLoader.getResource(String)``, it does not accept a ``File``, nor an URL, nor a webapp resource.
See below to use a `File` instead.
====

Add multiple `<scoreDrl>` elements if the score rules are split across multiple DRL files.

Optionally, you can also set drools configuration properties:

[source,xml,options="nowrap"]
----
  <scoreDirectorFactory>
    <scoreDrl>org/optaplanner/examples/nqueens/solver/nQueensScoreRules.drl</scoreDrl>
    <kieBaseConfigurationProperties>
      <drools.equalityBehavior>...</drools.equalityBehavior>
    </kieBaseConfigurationProperties>
  </scoreDirectorFactory>
----

To enable property reactive by default, without a `@propertyReactive` on the domain classes,
add `<drools.propertySpecific>ALWAYS</drools.propertySpecific>` in there.
Otherwise Planner automatically changes the Drools default to `ALLOWED` so property reactive is not active by default.


[[droolsScoreCalculationScoreDrlFile]]
=== A scoreDrlFile

To use `File` on the local file system, instead of a classpath resource, add the score rules DRL file in the solver configuration as a `<scoreDrlFile>` element:

[source,xml,options="nowrap"]
----
  <scoreDirectorFactory>
    <scoreDrlFile>/home/ge0ffrey/tmp/nQueensScoreRules.drl</scoreDrlFile>
  </scoreDirectorFactory>
----

[WARNING]
====
For portability reasons, a classpath resource is recommended over a File.
An application build on one computer, but used on another computer, might not find the file on the same location.
Worse, if they use a different Operating System, it is hard to choose a portable file path.
====

Add multiple `<scoreDrlFile>` elements if the score rules are split across multiple DRL files.


[[droolsScoreCalculationKsessionName]]
=== A ksessionName in a Kjar from a Maven repository

This way allows you to use score rules defined by the Workbench or build a kjar and deploy it to the Execution Server.
Both the score rules and the solver configuration are resources in a kjar.
Clients can obtain that kjar either from the local classpath, from a local Maven repository or even from a remote Maven repository.

The score rules still live in a DRL file, but the `KieContainer` finds that DRL file through the `META-INF/kmodule.xml` file:

[source,xml,options="nowrap"]
----
<kmodule xmlns="http://www.drools.org/xsd/kmodule">
  <configuration>
    <!-- Don't enable propertyReactive unless there is a @PropertyReactive annotation on the domain classes -->
    <property key="drools.propertySpecific" value="ALLOWED"/>
  </configuration>
  <kbase name="nQueensKbase" packages="org.optaplanner.examples.nqueens.solver">
    <ksession name="nQueensKsession"/>
  </kbase>
</kmodule>
----

The kmodule above will pick up all the DRL files in the package ``org.optaplanner.examples.nqueens.solver``.
A kbase can even extend another kbase.

[NOTE]
====
Starting from version 7.0, Drools enables property reactive by default for all classes.
This means if you have a non-simple getter and forget to apply `@Modifies` correctly, corruption occurs.
To avoid this, simply set `drools.propertySpecific` to `ALLOWED` as shown above.
====

Add the ksession name in the solver configuration as a `<ksessionName>` element:

[source,xml,options="nowrap"]
----
  <scoreDirectorFactory>
    <ksessionName>nQueensKsession</ksessionName>
  </scoreDirectorFactory>
----

In this approach, it's required to use a `SolverFactory.createFromKieContainerXmlResource(...)` method to <<solverConfigurationByXML,build the `SolverFactory`>>.
If no `<ksessionName>` element is specified, the default ksession of the `kmodule.xml` is used.


[[implementingAScoreRule]]
== Implementing a Score Rule

Here is an example of a score constraint implemented as a score rule in a DRL file:

[source,options="nowrap"]
----
rule "Horizontal conflict"
    when
        Queen($id : id, row != null, $i : rowIndex)
        Queen(id > $id, rowIndex == $i)
    then
        scoreHolder.addConstraintMatch(kcontext, -1);
end
----

This score rule will fire once for every two queens with the same ``rowIndex``.
The `(id > $id)` condition is needed to assure that for two queens A and B, it can only fire for (A, B) and not for (B, A), (A, A) or (B, B). Let us take a closer look at this score rule on this solution of four queens:

image::DroolsScoreCalculation/unsolvedNQueens04.png[align="center"]

In this solution the `Horizontal conflict` score rule will fire for six queen couples: (A, B), (A, C), (A, D), (B, C), (B, D) and (C, D). Because none of the queens are on the same vertical or diagonal line, this solution will have a score of ``-6``.
An optimal solution of four queens has a score of ``0``.

[NOTE]
====
Notice that every score rule uses at least one planning entity class
(directly or indirectly through a logically inserted fact).

It is a waste of time to write a score rule that only relates to problem facts,
as the consequence will never change during planning, no matter what the possible solution.
====

A `ScoreHolder` instance is asserted into the `KieSession` as a global called ``scoreHolder``.
The score rules need to (directly or indirectly) update that instance to influence the score of a solution state.

[NOTE]
====
The `kcontext` variable is a magic variable in Drools Expert.
The ``scoreHolder``'s method uses it to do incremental score calculation correctly and to create a `ConstraintMatch` instance.
====


[[weighingScoreRules]]
== Weighing Score Rules

If you've configured a <<constraintConfiguration,constraint configuration>>,
the score level and score weight of each constraint are beautifully decoupled from the constraint implementation,
so they can be changed by the business users more easily.

In that case, use the `reward()` and `penalize()` methods of the `ScoreHolder`:

[source,options="nowrap"]
----
package org.optaplanner.examples.nqueens.solver;
...
global SimpleScoreHolder scoreHolder;

rule "Horizontal conflict"
    when
        Queen($id : id, row != null, $i : rowIndex)
        Queen(id > $id, rowIndex == $i)
    then
        scoreHolder.penalize(kcontext);
end

// Vertical conflict is impossible due the model

rule "Ascending diagonal conflict"
    when
        Queen($id : id, row != null, $i : ascendingDiagonalIndex)
        Queen(id > $id, ascendingDiagonalIndex == $i)
    then
        scoreHolder.penalize(kcontext);
end

rule "Descending diagonal conflict"
    when
        Queen($id : id, row != null, $i : descendingDiagonalIndex)
        Queen(id > $id, descendingDiagonalIndex == $i)
    then
        scoreHolder.penalize(kcontext);
end
----

They automatically impact the score for each constraint match
by the score weight defined in the <<constraintConfiguration,constraint configuration>>.

The drl file must define a `package` (otherwise Drools defaults to `defaultpkg`)
and it must match with the <<constraintConfiguration,constraint configuration>>'s `constraintPackage`.

[NOTE]
====
To learn more about the Drools rule language (DRL),
consult https://drools.org/learn/documentation.html[the Drools documentation].
====

The score weight of some constraints depends on the constraint match.
In these cases, provide a match weight to the `reward()` or `penalize()` methods.
The score impact is the constraint weight multiplied with the match weight.

For example in conference scheduling, the impact of a content conflict,
depends on the number of shared content tags between 2 overlapping talks:

[source,options="nowrap"]
----
rule "Content conflict"
    when
        $talk1 : Talk(...)
        $talk2 : Talk(...)
    then
        scoreHolder.penalize(kcontext,
                $talk2.overlappingContentCount($talk1));
end
----

Presume its constraint weight is set to `100soft`.
So when 2 overlapping talks share only 1 content tag, the score is impacted by `-100soft`.
But when 2 overlapping talks share 3 content tags, the match weight is `3`, so the score is impacted by `-300soft`.

If there is no <<constraintConfiguration,constraint configuration>>,
you'll need to hard-code the weight in the constraint implementations:

[source,options="nowrap"]
----
global HardSoftScoreHolder scoreHolder;

// RoomCapacity: For each lecture, the number of students that attend the course must be less or equal
// than the number of seats of all the rooms that host its lectures.
rule "roomCapacity"
    when
        $room : Room($capacity : capacity)
        $lecture : Lecture(room == $room, studentSize > $capacity, $studentSize : studentSize)
    then
        // Each student above the capacity counts as one point of penalty.
        scoreHolder.addSoftConstraintMatch(kcontext, ($capacity - $studentSize));
end

// CurriculumCompactness: Lectures belonging to a curriculum should be adjacent
// to each other (i.e., in consecutive periods).
// For a given curriculum we account for a violation every time there is one lecture not adjacent
// to any other lecture within the same day.
rule "curriculumCompactness"
    when
        ...
    then
        // Each isolated lecture in a curriculum counts as two points of penalty.
        scoreHolder.addSoftConstraintMatch(kcontext, -2);
end
----

Notice how `addSoftConstraintMatch()` specifies that it's a soft constraint,
and needs a negative number to penalize each match. Otherwise it would reward such matches.
The parameter `($capacity - $studentSize)` always results into a negative number because `studentSize > $capacity`.
