import org.jenkinsci.plugins.workflow.libs.Library
@Library('jenkins-pipeline-shared-libraries')_

import org.kie.jenkins.MavenCommand

deployProperties = [:]
pipelineProperties = [:]

String optaplannerRepository = 'optaplanner'
String vehicleRoutingRepository = 'optaweb-vehicle-routing'
String employeeRosteringRepository = 'optaweb-employee-rostering'
String quickstartsRepository = 'optaplanner-quickstarts'

pipeline {
    agent {
        label 'kie-rhel7'
    }

    tools {
        maven 'kie-maven-3.6.2'
        jdk 'kie-jdk11'
    }

    options {
        buildDiscarder logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '', numToKeepStr: '10')
        timeout(time: 120, unit: 'MINUTES')
    }

    parameters {
        string(name: 'DISPLAY_NAME', defaultValue: '', description: 'Setup a specific build display name')

        // Deploy job url to retrieve deployment.properties
        string(name: 'DEPLOY_BUILD_URL', defaultValue: '', description: 'URL to jenkins deploy build to retrieve the `deployment.properties` file. If base parameters are defined, they will override the `deployment.properties` information')

        // Git information which can override `deployment.properties`
        string(name: 'BUILD_BRANCH_NAME', defaultValue: '', description: 'Override `deployment.properties`. Which branch to build? Set if you are not on a multibranch pipeline.')
        string(name: 'GIT_AUTHOR', defaultValue: '', description: 'Override `deployment.properties`. Which Git author repository ?')

        // Build&Deploy information for next snapshots
        string(name: 'MAVEN_SETTINGS_CONFIG_FILE_ID', defaultValue: 'kogito_release_settings', description: 'Maven settings configfile to use in pipeline for Maven commands')
        string(name: 'MAVEN_DEPENDENCIES_REPOSITORY', defaultValue: '', description: 'Maven repository where to find dependencies if those are not in the default Jboss repository.')
        string(name: 'MAVEN_DEPLOY_REPOSITORY', defaultValue: '', description: 'Specify a Maven repository to deploy the artifacts.')

        // Release information which can override `deployment.properties`
        booleanParam(name: 'RELEASE', defaultValue: false, description: 'Override `deployment.properties`. Is this build for a release?')

        string(name: 'PROJECT_VERSION', defaultValue: '', description: 'Override `deployment.properties`. Optional if not RELEASE. If RELEASE, cannot be empty.')
        string(name: 'KOGITO_VERSION', defaultValue: '', description: 'Optional if not RELEASE. If RELEASE, cannot be empty.')

        string(name: 'STAGING_REPO_URL', defaultValue: '', description: 'Override `deployment.properties`.')
        string(name: 'GIT_TAG', defaultValue: '', description: 'Git tag to set, if different from PROJECT_VERSION')

        // Bot author information. Set as params for easy testing.
        string(name: 'BOT_CREDENTIALS_ID', defaultValue: 'bsig-gh-bot', description: 'Credentials for PR creation')

        // Main author creds
        string(name: 'AUTHOR_CREDS_ID', defaultValue: 'kie-ci', description: 'Credentials for PR merge')
        string(name: 'GITHUB_TOKEN_CREDS_ID', defaultValue: 'kie-ci2-token', description: 'GH token to be used with GH CLI')
    }

    environment {
        PROPERTIES_FILE_NAME = 'deployment.properties'
        BOT_BRANCH_HASH = "${util.generateHash(10)}"
        GITHUB_CLI_VERSION = '0.11.1'
    }

    stages {
        stage('Initialization') {
            steps {
                script {
                    cleanWs()

                    if (params.DISPLAY_NAME != '') {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    readDeployProperties()

                    if (isRelease()) {
                        assert getProjectVersion() != ''
                        assert getKogitoVersion() != ''
                    }

                    installGithubCLI()
                }
            }
        }

        stage('Merge OptaPlanner deploy PR and tag') {
            when {
                expression { return isRelease() }
            }
            steps{
                script {
                    dir(optaplannerRepository) {
                        checkoutRepo(optaplannerRepository)
                        mergeAndPush(getDeployPrLink(optaplannerRepository))
                        tagLatest()
                    }
                }
            }
        }

        stage('Merge Optaweb Vehicle Routing deploy PR and tag') {
            when {
                expression { return isRelease() }
            }
            steps{
                script {
                    dir(vehicleRoutingRepository) {
                        checkoutRepo(vehicleRoutingRepository)
                        mergeAndPush(getDeployPrLink(vehicleRoutingRepository))
                        tagLatest()
                    }
                }
            }
        }

        stage('Merge Optaweb Employee Rostering deploy PR and tag') {
            when {
                expression { return isRelease() }
            }
            steps{
                script {
                    dir(employeeRosteringRepository) {
                        checkoutRepo(employeeRosteringRepository)
                        mergeAndPush(getDeployPrLink(employeeRosteringRepository))
                        tagLatest()
                    }
                }
            }
        }

        stage('Merge Quickstarts PR, tag and reset the stable branch') {
            when {
                expression { return isRelease() }
            }
            steps{
                script {
                    dir(quickstartsRepository) {
                        checkoutRepo(quickstartsRepository)
                        mergeAndPush(getDeployPrLink(quickstartsRepository))
                        tagLatest()

                        updateAndResetBranch(quickstartsRepository,'stable')
                    }
                }
            }
        }

        stage('Upload OptaPlanner documentation and distribution') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    getMavenCommand().inDirectory(optaplannerRepository).skipTests(true).withProperty('full').run('clean install')
                    uploadDistribution(optaplannerRepository)
                }
            }
        }

        stage('Upload Vehicle Routing documentation and distribution') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    getMavenCommand().inDirectory(vehicleRoutingRepository).skipTests(true).run('clean install')
                    uploadDistribution(vehicleRoutingRepository)
                }
            }
        }

        stage('Upload Employee Rostering documentation and distribution') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    getMavenCommand().inDirectory(employeeRosteringRepository).skipTests(true).run('clean install')
                    uploadDistribution(employeeRosteringRepository)
                }
            }
        }

        stage('Update OptaPlanner website') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    final String websiteRepository = 'optaplanner-website'
                    String prLink = null
                    dir("$websiteRepository-bot") {
                        String prBranchName = createWebsitePrBranch(websiteRepository)

                        // Update versions in links on the website.
                        sh "./build/update-versions.sh ${getProjectVersion()} ${getNextMinorSnapshotVersion(getProjectVersion())}"

                        // Update the XSDs. OptaPlanner must be cloned and build with the full profile before.
                        String optaplannerRoot = "$WORKSPACE/optaplanner"
                        sh "cp $optaplannerRoot/optaplanner-core/target/classes/solver.xsd xsd/solver/solver-8.xsd"
                        sh "cp $optaplannerRoot/optaplanner-benchmark/target/classes/benchmark.xsd xsd/benchmark/benchmark-8.xsd"

                        // Add changed files, commit, open and merge PR
                        prLink = commitAndCreatePR("Release OptaPlanner ${getProjectVersion()}", { sh 'git add xsd/\\*.xsd _config/pom.yml' }, prBranchName, 'master')
                    }
                    dir(websiteRepository) {
                        checkoutRepo(websiteRepository, 'master')
                        mergeAndPush(prLink, 'master')
                    }
                }
            }
        }

        stage('Set OptaPlanner next snapshot version') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    String nextMicroSnapshotVersion = getNextMicroSnapshotVersion(getProjectVersion())

                    dir("${optaplannerRepository}-bot") {
                        prepareForPR(optaplannerRepository)
                        String nextSnapshotVersion = getNextMicroSnapshotVersion(getProjectVersion())
                        maven.mvnVersionsSet(getMavenCommand(), nextSnapshotVersion, true)
                        maven.mvnSetVersionProperty(getMavenCommand(), 'version.org.kie.kogito', getNextMicroSnapshotVersion(getKogitoVersion()))

                        String prLink = commitAndCreatePR("[${getBuildBranch()}] Update snapshot version to ${nextMicroSnapshotVersion}")
                        setPipelinePrLink(optaplannerRepository, prLink)
                    }
                    dir(optaplannerRepository) {
                        sh "git checkout ${getBuildBranch()}"
                        mergeAndPush(getPipelinePrLink(optaplannerRepository))
                        runMavenDeploy(getMavenCommand())
                    }
                }
            }
        }

        stage('Set Optaweb Vehicle Routing next snapshot version') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    String nextMicroSnapshotVersion = getNextMicroSnapshotVersion(getProjectVersion())
                    dir("$vehicleRoutingRepository-bot") {
                        prepareForPR(vehicleRoutingRepository)
                        maven.mvnVersionsUpdateParentAndChildModules(nextMicroSnapshotVersion, true)

                        String prLink = commitAndCreatePR("[${getBuildBranch()}] Update snapshot version to ${nextMicroSnapshotVersion}")
                        setPipelinePrLink(vehicleRoutingRepository, prLink)
                    }
                    dir(vehicleRoutingRepository) {
                        sh "git checkout ${getBuildBranch()}"
                        mergeAndPush(getPipelinePrLink(vehicleRoutingRepository))
                        runMavenDeploy(getMavenCommand())
                    }
                }
            }
        }

        stage('Set Optaweb Employee Rostering next snapshot version') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    String nextMicroSnapshotVersion = getNextMicroSnapshotVersion(getProjectVersion())
                    dir("$employeeRosteringRepository-bot") {
                        prepareForPR(employeeRosteringRepository)
                        maven.mvnVersionsUpdateParentAndChildModules(nextMicroSnapshotVersion, true)

                        String prLink = commitAndCreatePR("[${getBuildBranch()}] Update snapshot version to ${nextMicroSnapshotVersion}")
                        setPipelinePrLink(employeeRosteringRepository, prLink)
                    }
                    dir(employeeRosteringRepository) {
                        sh "git checkout ${getBuildBranch()}"
                        mergeAndPush(getPipelinePrLink(employeeRosteringRepository))
                        runMavenDeploy(getMavenCommand())
                    }
                }
            }
        }

        stage('Set Quickstarts next snapshot version') {
            when {
                expression { return isRelease() }
            }
            steps {
                script {
                    String nextMicroSnapshotVersion = getNextMicroSnapshotVersion(getProjectVersion())
                    String botQuickstartsRepository = "${quickstartsRepository}-bot"
                    dir(botQuickstartsRepository) {
                        prepareForPR(quickstartsRepository)
                        updateQuickstartsVersions(nextMicroSnapshotVersion)

                        String prLink = commitAndCreatePR(("[${getBuildBranch()}] Update snapshot version to ${nextMicroSnapshotVersion}"));
                        setPipelinePrLink(quickstartsRepository, prLink)
                    }
                    dir(quickstartsRepository) {
                        sh "git checkout ${getBuildBranch()}"
                        mergeAndPush(getPipelinePrLink(quickstartsRepository))
                    }
                }
            }
        }
    }
    post {
        always {
            cleanWs()
        }
    }
}

//////////////////////////////////////////////////////////////////////////////
// Deployment properties
//////////////////////////////////////////////////////////////////////////////

void readDeployProperties(){
    String deployUrl = params.DEPLOY_BUILD_URL
    if (deployUrl != ''){
        if(!deployUrl.endsWith('/')){
            deployUrl += '/'
        }
        sh "wget ${deployUrl}artifact/${PROPERTIES_FILE_NAME} -O ${PROPERTIES_FILE_NAME}"
        deployProperties = readProperties file: PROPERTIES_FILE_NAME
        // echo all properties
        echo deployProperties.collect{ entry -> "${entry.key}=${entry.value}" }.join('\n')
    }
}

boolean hasDeployProperty(String key){
    return deployProperties[key] != null
}

String getDeployProperty(String key){
    if(hasDeployProperty(key)){
        return deployProperties[key]
    }
    return ''
}

String getParamOrDeployProperty(String paramKey, String deployPropertyKey){
    if (params[paramKey] != ''){
        return params[paramKey]
    }
    return getDeployProperty(deployPropertyKey)
}

//////////////////////////////////////////////////////////////////////////////
// Getter / Setter
//////////////////////////////////////////////////////////////////////////////

boolean isRelease() {
    return params.RELEASE || (getDeployProperty('release') == 'true')
}

String getProjectVersion() {
    return getParamOrDeployProperty('PROJECT_VERSION', 'project.version')
}

String getKogitoVersion() {
    return getParamOrDeployProperty('KOGITO_VERSION', 'kogito.version')
}

String getNextMicroSnapshotVersion(String currentVersion) {
    return util.getNextVersion(currentVersion, 'micro')
}

String getNextMinorSnapshotVersion(String currentVersion) {
    return util.getNextVersion(currentVersion, 'minor')
}

String getGitTag() {
    return params.GIT_TAG != '' ? params.GIT_TAG : getProjectVersion()
}

String getBuildBranch() {
    return getParamOrDeployProperty('BUILD_BRANCH_NAME', 'git.branch')
}

String getGitAuthor() {
    return getParamOrDeployProperty('GIT_AUTHOR', 'git.author')
}

String getGitAuthorCredsID(){
    return params.AUTHOR_CREDS_ID
}

String getBotAuthorCredsID(){
    return params.BOT_CREDENTIALS_ID
}

String getStagingRepoUrl(){
    return getParamOrDeployProperty('STAGING_REPO_URL', 'staging-repo.url')
}

String getDeployPrLink(String repo){
    return getDeployProperty("${repo}.pr.link")
}

String getPipelinePrLink(String repo){
    return pipelineProperties["${repo}.pr.link"]
}

void setPipelinePrLink(String repo, String value){
    pipelineProperties["${repo}.pr.link"] = value
}

String getSnapshotBranch(){
    return "${getNextMicroSnapshotVersion(getProjectVersion()).toLowerCase()}-${env.BOT_BRANCH_HASH}"
}

//////////////////////////////////////////////////////////////////////////////
// Git
//////////////////////////////////////////////////////////////////////////////

void checkoutRepo(String repo, String branch) {
    deleteDir()
    checkout(githubscm.resolveRepository(repo, getGitAuthor(), branch, false))
    // need to manually checkout branch since on a detached branch after checkout command
    sh "git checkout ${branch}"
}

void checkoutRepo(String repo) {
    checkoutRepo(repo, getBuildBranch())
}

void mergeAndPush(String prLink, String targetBranch) {
    if (prLink != '') {
        githubscm.mergePR(prLink, getGitAuthorCredsID())
        githubscm.pushObject('origin', targetBranch, getGitAuthorCredsID())
    }
}

void mergeAndPush(String prLink) {
    mergeAndPush(prLink, getBuildBranch())
}

void tagLatest() {
    if (getGitTag() != '') {
        githubscm.tagRepository(getGitTag(), env.BUILD_TAG)
        githubscm.pushObject('origin', "--tags ${getGitTag()}", getGitAuthorCredsID())
    }
}

void prepareForPR(String repo) {
    checkoutRepo(repo)
    githubscm.forkRepo(getBotAuthorCredsID())
    githubscm.createBranch(getSnapshotBranch())
}

String commitAndCreatePR(String commitMsg, Closure precommit, String localBranch, String targetBranch) {
    def prBody = "Generated by build ${BUILD_TAG}: ${BUILD_URL}"

    githubscm.commitChanges(commitMsg, precommit)
    githubscm.pushObject('origin', localBranch, getBotAuthorCredsID())
    return githubscm.createPR(commitMsg, prBody, targetBranch, getBotAuthorCredsID())
}

String commitAndCreatePR(String commitMsg) {
    return commitAndCreatePR(commitMsg, {
        githubscm.findAndStageNotIgnoredFiles('pom.xml')
        githubscm.findAndStageNotIgnoredFiles('build.gradle')
    }, getSnapshotBranch(), getBuildBranch())
}

String createWebsitePrBranch(String websiteRepository) {
    checkoutRepo(websiteRepository, 'master') // there is no other branch
    githubscm.forkRepo(getBotAuthorCredsID())
    String prBranchName = "${getProjectVersion().toLowerCase()}-${env.BOT_BRANCH_HASH}"
    githubscm.createBranch(prBranchName)
    return prBranchName
}

void installGithubCLI() {
    sh """
    wget https://github.com/cli/cli/releases/download/v${env.GITHUB_CLI_VERSION}/gh_${env.GITHUB_CLI_VERSION}_linux_amd64.tar.gz
    tar xzf gh_${env.GITHUB_CLI_VERSION}_linux_amd64.tar.gz
    mv gh_${env.GITHUB_CLI_VERSION}_linux_amd64/bin/gh .
    rm -r gh_${env.GITHUB_CLI_VERSION}_linux_amd64*
    """
}

void uploadDistribution(String directory) {
    dir(directory) {
        echo "uploadDistribution for ${directory}"
        withCredentials(bindings: [sshUserPrivateKey(credentialsId: 'optaplanner-filemgmt',
                keyFileVariable: 'SSH_KEY_JBOSS_FILEMGMT')]) {
            // For testing, simulate connection via SSH:
            // sh "ssh -i $SSH_KEY_JBOSS_FILEMGMT -oKexAlgorithms=+diffie-hellman-group1-sha1 optaplanner@filemgmt.jboss.org"
            sh "./build/release/upload_distribution.sh ${getProjectVersion()} $SSH_KEY_JBOSS_FILEMGMT"
        }
    }
}

MavenCommand getMavenCommand() {
    mvnCmd = new MavenCommand(this, ['-fae'])
                    .withSettingsXmlId(params.MAVEN_SETTINGS_CONFIG_FILE_ID)
    if (params.MAVEN_DEPENDENCIES_REPOSITORY) {
        mvnCmd.withDependencyRepositoryInSettings('deps-repo', params.MAVEN_DEPENDENCIES_REPOSITORY)
    }
    return mvnCmd
}

void runMavenDeploy(MavenCommand mvnCmd) {
    mvnCmd = mvnCmd.clone()
    if(params.MAVEN_DEPLOY_REPOSITORY){
        mvnCmd.withDeployRepository(params.MAVEN_DEPLOY_REPOSITORY)
    }
    mvnCmd.skipTests(true).run('clean deploy')
}

def updateAndResetBranch(String repo, String branchToReset) {
    githubscm.createBranch(branchToReset)

    removeJbossNexusFromMavenAndGradle()

    githubscm.commitChanges("[${getBuildBranch()}] Update ${branchToReset} to ${getProjectVersion()}", {
        githubscm.findAndStageNotIgnoredFiles('pom.xml')
        githubscm.findAndStageNotIgnoredFiles('build.gradle')
    })
    forcePushProtectedBranch(repo, branchToReset)
}

void removeJbossNexusFromMavenAndGradle() {
    sh "sed -i \':a;N;\$!ba;s/\\n *<repositories>.*<\\/repositories>//g\' */pom.xml"
    sh "sed -i \':a;N;\$!ba;s/\\n *<repositories>.*<\\/repositories>//g\' pom.xml"
    sh "sed -i \':a;N;\$!ba;s/\\n *\\/\\/ To get snapshots during development[^{}]*maven {[^{}]*mavenContent " +
            "{[^{}]*snapshotsOnly[^{}]*}[^{}]*}//g\' */build.gradle"

    assert !sh (script:
            'grep -Rn "repository.jboss.org" --include={pom.xml,build.gradle} | ' +
            'cat', returnStdout: true)
}

String getGhCredsID() {
    return params.GITHUB_TOKEN_CREDS_ID;
}

//maps git branch protection response json into update request body to rewrite branch protection
String getProtectionMapScript() {
    return "if .restrictions == null then . + {\"restrictions_used\":null}  " + //upd user set restrictions if not empty
            "else . + {\"restrictions_used\":{\"users\":[.restrictions.users[].login], \"team\":[.restrictions.users[].slug]}} end |" +
            " {" +
                "\"required_status_checks\": .required_status_checks," +
                "\"required_pull_request_reviews\": " +
                    "{" +
                        "\"dismiss_stale_reviews\":(.required_pull_request_reviews.dismiss_stale_reviews//false), " +
                        "\"require_code_owner_reviews\":(.required_pull_request_reviews.require_code_owner_reviews//false)" +
                    "}, " +
                "\"enforce_admins\":(.enforce_admins.enabled)," +
                " \"restrictions\":.restrictions_used" +
            "}";
}

def enableForcePushes(String repo, String protectedBranch) {
    setAllowForcePushes(repo, protectedBranch, "true");
}

def disableForcePushes(String repo, String protectedBranch) {
    setAllowForcePushes(repo, protectedBranch, "false");
}

def setAllowForcePushes(String repo, String protectedBranch, String enabled, String ghPath = "../gh") {
    sh "if [ -x $ghPath ]; then echo 'gh found at $ghPath'; else echo 'gh not found at $ghPath' && exit 1; fi"
    //Use separate admin token credentials
    withCredentials([string(credentialsId: getGhCredsID(), variable: 'GITHUB_TOKEN')]) {
        //get current branch protection and remove allow force push
        sh "${ghPath} api 'repos/${getGitAuthor()}/${repo}/branches/${protectedBranch}/protection' | " +
                "jq 'del(.allow_force_pushes)' > protectionBefore.json";

        //create new json based on current protection mapped as parameters
        sh "jq . protectionBefore.json | " +
           "jq \"${getProtectionMapScript()}\" | " +
           "jq \". + {\"allow_force_pushes\":${enabled}}\" > protectionParameters.json";

        //update protection on git
        def allowForcePushEnabled = sh(script:
                "${ghPath} api -XPUT 'repos/${getGitAuthor()}/${repo}/branches/${protectedBranch}/protection' " +
                        "--input protectionParameters.json |" +
                        " jq '.allow_force_pushes.enabled'", returnStdout: true).trim();

        //check that protection didn't changed except for allow_force_pushes
        sh "${ghPath} api 'repos/${getGitAuthor()}/${repo}/branches/${protectedBranch}/protection' | " +
                "jq 'del(.allow_force_pushes)' > protectionAfter.json";
        assert !sh (script: "diff protectionBefore.json protectionAfter.json", returnStdout: true)
        sh "rm -f protectionParameters.json protectionBefore.json protectionAfter.json"

        if (enabled == allowForcePushEnabled) {
            echo "[INFO] ${getGitAuthor()}/${repo}'s ${protectedBranch} force push enabled."
        } else {
            error "Couldn\'t change protection on ${protectedBranch}."
        }
    }
}

def forcePushProtectedBranch(String repo, String protectedBranch) {
    enableForcePushes(repo, protectedBranch)
    withCredentials([usernamePassword(credentialsId: getGitAuthorCredsID(), usernameVariable: 'GIT_USERNAME', passwordVariable: 'GIT_PASSWORD')]) {
        sh """
        git config --local credential.helper \"!f() { echo username=\\$GIT_USERNAME; echo password=\\$GIT_PASSWORD; }; f\"
        git push origin ${protectedBranch} --force
        """
    }
    disableForcePushes(repo, protectedBranch)
}

void gradleVersionsUpdate(String newVersion) {
    sh "sed -i -E 's/def optaplannerVersion = \"[^\"\\s]+\"/def optaplannerVersion = \"${newVersion}\"/' */build.gradle"
}

void updateQuickstartsVersions(String newVersion) {
    maven.mvnSetVersionProperty("version.org.optaplanner", newVersion)
    maven.mvnVersionsUpdateParentAndChildModules(newVersion, true)
    gradleVersionsUpdate(newVersion)

    assert !sh(script:
            "grep -Rn \"${getProjectVersion()}\" --include={pom.xml,build.gradle} | " +
             "cat", returnStdout: true)
}
